# Maintainer: Albert I <kras@raphielgang.org>

pkgbase=slot-plasma-themes
pkgname=(
    gtk-theme-slot-dark
    kvantum-theme-slot-dark
    plasma5-themes-slot-dark  # keep packaging for time being
    plasma6-themes-slot-dark
    sddm-theme-slot
    slot-icon-theme
    slot-icon-theme-extra  # split up extra variants
)
pkgver=4.3_20250820
pkgrel=1
epoch=1
pkgdesc='Slot themes for Plasma desktop'
arch=(any)
url='https://github.com/L4ki/Slot-Plasma-Themes'
license=(GPL-3.0-only)
# We need these to tone down icon themes size a bit
makedepends=(
    rdfind
    symlinks
)
options=(!strip !emptydirs)
_commit=9532e97e3a4bfb4324d7e2a802bc78524edbcaec
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/${_commit}.tar.gz")
noextract=("${pkgbase}-${pkgver}.tar.gz")
sha384sums=('2359d48e14c41d4c86b0668a18ad2e74baf7ae66ffa380fe8a8ab3322247768d31dfef0322649d3130760c8cb642e7c9')
b2sums=('b705346696089e0ddcf6cdd5f92e7d2d6f7b6abab568eb81f2912839050e8200fd9587e38a6c4625605a0f68d70ead32337068891e1dda25a51aa23a29cb66f1')

_install_icons() {
    local _base _iconsdir _path _theme
    _iconsdir=${pkgdir}/usr/share/icons
    _theme=$1
    _base=$2

    install -d "${_iconsdir}"
    echo "Installing ${_theme} theme..."
    cp -r -v --no-preserve=all "${_base:-${_theme}}" "${_iconsdir}/${_theme}"
    if [[ -n ${_base} ]]; then
        find "${_theme}" -mindepth 1 -maxdepth 1 -type d | while IFS= read -r _path; do
            cp -f -r -v --no-preserve=all "${_path}" "${_iconsdir}/${_theme}/"
        done
        sed "s/${_base}/${_theme}/g" -i "${_iconsdir}/${_theme}/index.theme"
    fi
    rdfind -checksum sha512 -makesymlinks true -makeresultsfile false "${_iconsdir}/${_theme}"
    symlinks -c -r "${_iconsdir}/${_theme}"
}

prepare() {
    mkdir "${pkgbase}-${pkgver}"
    tar xf "${pkgbase}-${pkgver}.tar.gz" -C "${pkgbase}-${pkgver}" --strip-components 1
    cd "${srcdir}/${pkgbase}-${pkgver}"

    # Remove junk files
    find . \( -name 32.files -o -name 48.files -o -name 'Text File.txt' -o -name icon-theme.cache \
        -o -name make-symlinks.sh \) -delete
    rm -f 'Slot Plasma Themes/Slot-Dark-Plasma/'{AUTHORS,LICENSE}

    cd "${srcdir}/${pkgbase}-${pkgver}/Slot Global Themes"

    # Use correct window decoration theme for Gradient-Dark theme
    sed 's/Gradient-Color-Dark-Aurorae/Slot-Dark-Aurorae-5/' \
        -i Slot-Gradient-Dark-Global-5/contents/defaults
    sed 's/Gradient-Color/Slot/' -i Slot-Gradient-Dark-Global-6/contents/defaults

    cd "${srcdir}/${pkgbase}-${pkgver}/Slot Icons Themes"

    # Symlink any @2x, @3x, ... to their original non-scaled icons
    find . -type f \( -name 16 -o -name 22 -o -name 24 \) | while IFS= read -r _fixup; do
        rm -r "$(dirname "${_fixup}")"
        ln -s "$(basename "${_fixup}")" "$(dirname "${_fixup}")"
    done

    # Get rid of any incorrect files present on icon themes
    find . -type f ! \( -iname '*.svg' -o -name index.theme \) -delete

    # Move some icons to their proper place
    mkdir Slot-Blue-Light-Icons/places
    mv -t Slot-Blue-Light-Icons/places Slot-Blue-Light-Icons/{32,64}
    mv Slot-Light-Icons/*.svg Slot-Light-Icons/mimetypes/64
}

package_gtk-theme-slot-dark() {
    pkgdesc+=' - GTK+ themes'
    depends=(gtk{3,4})
    optdepends=('gtk-engine-murrine: for using GTK2 theme')

    pushd "${pkgbase}-${pkgver}/Slot GTK Themes"
    install -d "${pkgdir}/usr/share/themes"
    cp -r -v --no-preserve=all Slot-Dark-GTK "${pkgdir}/usr/share/themes"
    popd
}

package_kvantum-theme-slot-dark() {
    pkgdesc+=' - Kvantum theme'
    depends=(kvantum)

    pushd "${pkgbase}-${pkgver}/Slot Kvantum Themes"
    install -d "${pkgdir}/usr/share/Kvantum"
    cp -r -v --no-preserve=all Slot-Dark-Kvantum "${pkgdir}/usr/share/Kvantum"
    popd
}

package_plasma5-themes-slot-dark() {
    pkgdesc+=' - for KDE Plasma 5'
    optdepends=(
        'gtk-theme-slot-dark: for matching GTK+ theme'
        'kvantum-theme-slot-dark: for matching Kvantum theme'
        'sddm-theme-slot: for matching SDDM theme'
        'slot-icon-theme: for matching icon theme'
    )
    # Conflict with plasma6-themes due to shared components
    conflicts=(plasma6-themes-slot-dark)

    cd "${pkgbase}-${pkgver}"

    pushd 'Slot Window Decorations'
    install -d "${pkgdir}/usr/share/aurorae/themes"
    cp -r -v --no-preserve=all -t "${pkgdir}/usr/share/aurorae/themes" Slot-{Blur-,}Dark-Aurorae-5
    popd

    pushd 'Slot Color Schemes'
    install -d "${pkgdir}/usr/share/color-schemes"
    cp -r -v --no-preserve=all SlotDarkColorscheme.colors "${pkgdir}/usr/share/color-schemes"
    popd

    pushd 'Slot Plasma Themes'
    install -d "${pkgdir}/usr/share/plasma/desktoptheme"
    cp -r -v --no-preserve=all Slot-Dark-Plasma "${pkgdir}/usr/share/plasma/desktoptheme"
    rm -f "${pkgdir}/usr/share/plasma/desktoptheme/Slot-Dark-Plasma/metadata.json"
    popd

    pushd 'Slot Global Themes'
    install -d "${pkgdir}/usr/share/plasma/look-and-feel"
    cp -r -v --no-preserve=all -t "${pkgdir}/usr/share/plasma/look-and-feel" \
        Slot-{,Gradient-}Dark-Global-5
    popd

    install -d "${pkgdir}/usr/share/wallpapers"
    cp -r -v --no-preserve=all 'Slot Wallpapers' "${pkgdir}/usr/share/wallpapers/Slot-Wallpapers"
}

package_plasma6-themes-slot-dark() {
    pkgdesc+=' - for KDE Plasma 6'
    optdepends=(
        'gtk-theme-slot-dark: for matching GTK+ theme'
        'kvantum-theme-slot-dark: for matching Kvantum theme'
        'sddm-theme-slot: for matching SDDM theme'
        'slot-icon-theme: for matching icon theme'
    )
    # Conflict with plasma5-themes due to shared components
    conflicts=(plasma5-themes-slot-dark)

    cd "${pkgbase}-${pkgver}"

    pushd 'Slot Window Decorations'
    install -d "${pkgdir}/usr/share/aurorae/themes"
    cp -r -v --no-preserve=all -t "${pkgdir}/usr/share/aurorae/themes" Slot-{Blur-,}Dark-Aurorae-6
    popd

    pushd 'Slot Color Schemes'
    install -d "${pkgdir}/usr/share/color-schemes"
    cp -r -v --no-preserve=all SlotDarkColorscheme.colors "${pkgdir}/usr/share/color-schemes"
    popd

    pushd 'Slot Plasma Themes'
    install -d "${pkgdir}/usr/share/plasma/desktoptheme"
    cp -r -v --no-preserve=all Slot-Dark-Plasma "${pkgdir}/usr/share/plasma/desktoptheme"
    rm -f "${pkgdir}/usr/share/plasma/desktoptheme/Slot-Dark-Plasma/metadata.desktop"
    popd

    pushd 'Slot Global Themes'
    install -d "${pkgdir}/usr/share/plasma/look-and-feel"
    cp -r -v --no-preserve=all -t "${pkgdir}/usr/share/plasma/look-and-feel" \
        Slot-{,Gradient-}Dark-Global-6
    popd

    install -d "${pkgdir}/usr/share/wallpapers"
    cp -r -v --no-preserve=all "Slot Wallpapers" "${pkgdir}/usr/share/wallpapers/Slot-Wallpapers"
}

package_sddm-theme-slot() {
    pkgdesc+=' - SDDM theme'
    depends=(sddm)

    pushd "${pkgbase}-${pkgver}/Slot SDDM Themes"
    install -d "${pkgdir}/usr/share/sddm/themes"
    cp -r -v --no-preserve=all Slot-SDDM-6 "${pkgdir}/usr/share/sddm/themes"
    popd
}

package_slot-icon-theme() {
    pkgdesc+=' - icon themes'
    depends=(
        breeze-icons
        hicolor-icon-theme
    )
    optdepends=('slot-icon-theme-extra: additional icon theme variants')

    pushd "${pkgbase}-${pkgver}/Slot Icons Themes"
    _install_icons Slot-Dark-Icons
    _install_icons Slot-Gradient-Dark-Icons
    popd
}

package_slot-icon-theme-extra() {
    pkgdesc+=' - extra icon themes'
    depends=(
        breeze-icons
        hicolor-icon-theme
    )

    pushd "${pkgbase}-${pkgver}/Slot Icons Themes"
    _install_icons Slot-Light-Icons
    _install_icons Slot-Blue-Dark-Icons Slot-Dark-Icons
    _install_icons Slot-Color-Dark-Icons Slot-Dark-Icons
    _install_icons Slot-Multicolor-Dark-Icons Slot-Dark-Icons
    _install_icons Slot-Symbolic-Dark-Icons Slot-Dark-Icons
    _install_icons Slot-Blue-Light-Icons Slot-Light-Icons
    _install_icons Slot-Color-Light-Icons Slot-Light-Icons
    _install_icons Slot-Gradient-Light-Icons Slot-Light-Icons
    _install_icons Slot-Multicolor-Light-Icons Slot-Light-Icons
    _install_icons Slot-Symbolic-Light-Icons Slot-Light-Icons
    popd
}
