# Maintainer: Albert I <kras@raphielgang.org>

# Based on original lrzip PKGBUILD by:
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: graysky <graysky@archlinux.us>
# Contributor: <kastor@fobos.org.ar>

pkgname=lrzip-next
pkgver=0.14.1
pkgrel=1
pkgdesc='Next generation of multi-threaded compression based on rzip'
url='https://github.com/pete4abw/lrzip-next'
license=('GPL-2.0-or-later')
arch=('x86_64')
depends=(
    'bash'
    'bzip2' 'libbz2.so'
    'bzip3' 'libbzip3.so'
    'gcc-libs'
    'glibc'
    'libgcrypt'
    'libgpg-error'
    'lz4'
    'lzo' 'liblzo2.so'
    'zlib' 'libz.so'
    'zstd' 'libzstd.so'
)
provides=('lrzip')
conflicts=('lrzip')
source=("${pkgname}::git+${url}.git#tag=v${pkgver}?signed")
sha384sums=('48f270f2a40ed99546ce852c4afd2182998756c8646350ee4d3e08c4c8be2b14df1c9cf1f84f1a32e3364304597f4bfe')
b2sums=('f2902a8d18084fd729fc31a4b3171e592a356448a62253fbecfc9e4d5b4990a9050f8edd83db477ab71de19d54c419c603676144c1363702714f64b230f9da8f')
validpgpkeys=('6EFBD2123240FF8E3787274CEE344AF5EB2C5812')  # Peter Hyman (2022 Key) <pete@peterhyman.com>

build() {
  cd ${pkgname}

  CFLAGS="$CFLAGS -fomit-frame-pointer"
  CXXFLAGS="$CXXFLAGS -fomit-frame-pointer"

  ./autogen.sh --prefix=/usr "${flags}"
  make
}

package() {
  DESTDIR="${pkgdir}" make -C ${pkgname} install-strip
  ln -s lrzip-next "${pkgdir}/usr/bin/lrzip"
}

# vim: ts=2 sw=2 et:
# getver: -u 3 github.com/pete4abw/lrzip-next/blob/master/ChangeLog
