pkgname=we10x-icon-theme
pkgver=20250810
pkgrel=1
pkgdesc='A colorful design icon theme for Linux desktop'
arch=(any)
url='https://store.kde.org/p/1366371'
license=(GPL-3.0-only)  # CHECKME
groups=(we10xos-theme)
makedepends=(git)
options=(!strip)
_commit=698545387d335ee348d7836baae52ce5669231f2
source=("${pkgname}-${_commit}.tar.gz::https://github.com/yeyushengfan258/We10X-icon-theme/archive/${_commit}.tar.gz")
sha384sums=('1dad2d8a43732f3309a0d25ac19e8697bd013a903e4c87094b84884eba9350cba3835aaa4ea2efda7365834d03dcabc9')
b2sums=('c5f613a47780b1eccec860e59370191395d09dc3b92778e2fc217876bd1159ebb214bea9e0e5ac412f6b09add97cc972f56bbfa98445f12b615f3fc361cd2ff0')

prepare() {
    # Don't trigger icon cache update when installing
    sed '/gtk-update-icon-cache/d' -i We10X-icon-theme-${_commit}/install.sh
}

package() {
    cd We10X-icon-theme-${_commit}
    ./install.sh -d "${pkgdir}/usr/share/icons" -t all
    # This removes broken symlinks created by the script
    find "${pkgdir}/usr/share/icons" -type l \
        -exec sh -c 'for x; do [[ -e $x ]] || rm -v -f "$x"; done' _ {} +
}
