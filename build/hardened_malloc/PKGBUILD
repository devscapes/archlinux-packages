# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Thibaut Sautereau (thithib) <thibaut at sautereau dot fr>

pkgname=hardened_malloc
pkgver=13.r7.g7481c88
pkgrel=2
pkgdesc='Hardened allocator designed for modern systems'
arch=(
    aarch64
    x86_64
)
url='https://github.com/GrapheneOS/hardened_malloc'
license=(MIT)
depends=(
    gcc-libs
    glibc
)
makedepends=(git)
optdepends=(
    # https://github.com/GrapheneOS/hardened_malloc#compatibility; not really required
    'openssh: mprotect PROT_READ|PROT_WRITE syscalls in seccomp-bpf filter'
)
checkdepends=(
    python  # for test_smc.py
)
provides=(libhardened_malloc{,-{light,desktop}}="${pkgver%%.*}")
_commit=7481c8857faf5c6ed8666548d9e92837693de91b  # check GitHub Actions before bumping revision
source=("${pkgname}::git+${url}.git#commit=${_commit}")
sha384sums=('9511ef3873450eb19b789d131bbbe1c76618141e0f5106803dfe9a60e4dc66ced49845589bce8fec730f49ece9b11ffe')
b2sums=('7bff7839cd3bbe535c3c4d631c8defebc16b9b9dd9c2db334936601190d5213d8187ea34cbafaaa8f82ad886c06af287ebde48bac3f2592ec26ad6e25de2c7c2')

pkgver() {
    # Avoid matching GrapheneOS Android release tags
    git -C "${pkgname}" describe --long --match '[0-9][! -]' HEAD | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd ${pkgname}
    # Create a clone of -light variant config that also disables its drop-in C++ allocator
    sed '/CXX_ALLOCATOR/s/true/false/' config/light.mk > config/desktop.mk
}

build() {
    local -a _common_flags=(
        'CONFIG_EXAMPLE=false'
        'CONFIG_NATIVE=false'
        'CONFIG_WERROR=false'
    )

    make -C ${pkgname} "${_common_flags[@]}" VARIANT=default
    make -C ${pkgname} "${_common_flags[@]}" VARIANT=light
    make -C ${pkgname} "${_common_flags[@]}" VARIANT=desktop
}

check() {
    # Testing non-default variants are not yet supported
    make -C ${pkgname} test
}

package() {
    cd ${pkgname}
    install -D -t "${pkgdir}/usr/lib" \
        -m755 out/libhardened_malloc.so \
        -m755 out-light/libhardened_malloc-light.so \
        -m755 out-desktop/libhardened_malloc-desktop.so
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
