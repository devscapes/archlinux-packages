# Maintainer: Albert I <kras@raphielgang.org>
# Contributor: Jan0660 <jan0660@tutanota.com>

pkgname=oh-my-posh
pkgver=26.19.0
pkgrel=1
pkgdesc='A prompt theme engine for any shell'
arch=(
    aarch64
    armv7h
    i686
    pentium4
    x86_64
)
url='https://ohmyposh.dev'
license=(MIT)
depends=(
    glibc
)
makedepends=(
    git
    go
)
source=("${pkgname}::git+https://github.com/JanDeDobbeleer/oh-my-posh.git#tag=v${pkgver}")
sha384sums=('3a372782df6719cdbfe8164cdcd5700fe27fc8c7a180e1b114d61fb5537aa930f03b5ab7456d514dbef517c03747bc75')
b2sums=('ae2572dc792c96ec7ef867bd639a5f53946c03f18ff22c550dc32ace5a69565cd0b95b05b232e376694a8c11613f57dc86d42dd42d53c647a10259062d62f3ff')

prepare() {
    cd ${pkgname}/src
    GOFLAGS='-mod=readonly' go mod vendor -v

    # Go builder isn't happy with date string we need to pass; this helps with reproducibility
    export SOURCE_DATE_EPOCH=$(git log -1 --pretty=%ct)
    local _date=$(date -u -d "@${SOURCE_DATE_EPOCH}" +'%Y-%m-%d %H:%M:%S.%N %z %Z')
    sed -e '/import "time"/d' \
        -e "/Date/s/= .*$/= \"${_date}\"/" \
        -e "/Version/s/= .*$/= \"${pkgver}\"/" \
        -i build/version.go
}

build() {
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS='-buildmode=pie -trimpath -mod=vendor -modcacherw -ldflags=-linkmode=external'

    cd ${pkgname}/src
    go build -v -o ${pkgname} .
}

package() {
    cd ${pkgname}
    install -Dm755 src/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
    install -Dt "${pkgdir}/usr/share/${pkgname}/themes" -m644 themes/*.omp.*
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
