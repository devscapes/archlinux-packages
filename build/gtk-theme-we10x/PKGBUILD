# Maintainer: Albert I <kras@raphielgang.org>

pkgbase=gtk-theme-we10x
pkgname=(
    cinnamon-theme-we10x
    gnome-shell-theme-we10x
    gtk-theme-we10x
)
pkgver=20250520
pkgrel=1
arch=(any)
url='https://www.gnome-look.org/s/Gnome/p/2275817'
license=(GPL-3.0-only)  # CHECKME
groups=(we10xos-theme)
makedepends=(
    gnome-shell
    sassc
)
options=(!strip)
_commit=ee2475ecbd35bb5b9f28407a21ce1c005b4db663
source=("We10X-gtk-theme-${_commit}.tar.gz::https://github.com/yeyushengfan258/We10X-gtk-theme/archive/${_commit}.tar.gz")
sha384sums=('ea50b1a75afa6ccc0939f77b3f2271a983ba89000740150f50a99611811bfce6e68ac3f3512ed705f1fb8efd263a1807')
b2sums=('f90f7a27121c05c54d51ba87d024e0ec687ba3fda0274a8d09fd4423e37009d9ede1ae75c07a492d092995c2bb4ec07afcb9b9b7720aee478e3d5d264f501a74')

_install() {
    local _height _heights _realpath _theme _type _variant _variants
    _realpath=${1}
    [[ -z ${2} ]] || _type=${2}
    _variants=('' -Dark)
    _heights=('' -compact)

    for _variant in "${_variants[@]}"; do
        for _height in "${_heights[@]}"; do
            _theme=We10X${_variant}${_height}
            install -v -d "${_realpath}/${_theme}"
            mv -v "tmp-install/${_theme}/${_type}" "${_realpath}/${_theme}"
        done
    done
}

prepare() {
    cd "We10X-gtk-theme-${_commit}"

    # DO NOT install GTK2, Metacity, Plank and Xfwm4 themes â€” use fluent-gtk-theme
    sed '/MetacityTheme/d;/gtk-2.0/d;/xfwm4/,/plank\/theme/d' -i install.sh

    # We don't need extra copies of license files
    sed '/COPYING/d' -i install.sh

    # Regenerate fresh CSS files
    find . -iname 'cinnamon*.css' -o -iname 'gnome-shell*.css' -o -iname 'gtk*.css' -delete
    sed '/which sassc/s/.*/if false; then/' parse-sass.sh | bash
}

build() {
    cd "We10X-gtk-theme-${_commit}"
    # Install to a temporary path to allow creation of split packages
    ./install.sh -d "${srcdir}/tmp-install" -c standard dark
}

package_cinnamon-theme-we10x() {
    pkgdesc='Fluent design theme for Cinnamon desktop'
    depends=(
        gtk-theme-we10x
        cinnamon
    )
    optdepends=(
        'we10x-icon-theme: matching icon theme'
        'xcursor-win10os: matching cursor theme'
    )

    _install "${pkgdir}/usr/share/themes" cinnamon
}

package_gnome-shell-theme-we10x() {
    pkgdesc='Fluent design theme for GNOME Shell'
    depends=(
        gtk-theme-we10x
        'gnome-shell>=48'
        gnome-themes-extra
    )
    optdepends=(
        'we10x-icon-theme: matching icon theme'
        'xcursor-win10os: matching cursor theme'
    )

    _install "${pkgdir}/usr/share/themes" gnome-shell
    _install "${pkgdir}/usr/share/themes" index.theme
}

package_gtk-theme-we10x() {
    pkgdesc='Fluent design theme for GTK3/4-based desktops'
    depends=(
        'gtk3>=3.20'
        gtk4
    )
    optdepends=(
        'cinnamon-theme-we10x: for use with Cinnamon desktop'
        'fluent-gtk-theme: for use with GNOME Flashback, MATE and Xfce desktops'
        'gnome-shell-theme-we10x: for use with GNOME Shell'
        'kde-gtk-config: for use as GTK Application Style with KDE Plasma'
    )

    install -v -d "${pkgdir}/usr/share"
    mv -v tmp-install "${pkgdir}/usr/share/themes"
}
